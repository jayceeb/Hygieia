plugins {
  id "com.moowork.node" version "0.10"
}

apply plugin: 'com.moowork.node'

// todo: rename directory
archivesBaseName = "hygieia-ui"


node {
  // Version of node to use.
  version = '0.12.0'

  // Version of npm to use.
  npmVersion = '2.7.1'

  // Base URL for fetching node distributions (change if you have a mirror).
  distBaseUrl = 'http://nodejs.org/dist'

  // If true, it will download node using above parameters.
  // If false, it will try to use globally installed node.
  download = true

  // Set the work directory for unpacking node
  workDir = file("${project.buildDir}/node_tmp")

  // Set the work directory where node_modules should be located
  nodeModulesDir = file("${project.projectDir}")
}

// bower
task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('bower_components')
    dependsOn npm_install
}

// gulp
task gulpInstall(type: NodeTask) {
    script = file('node_modules/gulp/bin/gulp.js')
    args = ['build']
    dependsOn bowerInstall
}

task run(type: NodeTask) {
  script = file('node_modules/gulp/bin/gulp.js')
  args = ['serve']
  dependsOn bowerInstall
}

tasks.build.dependsOn(gulpInstall)
clean.delete << file('node_modules')
clean.delete << file('bower_components')


dependencies {
  compile project(':common')
}
